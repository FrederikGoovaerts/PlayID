;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;

;;; A Variant of the Colonel Blotto Game (by Sam Schreiber)

;;;

;;; In this game, you start with 20 armies, and go through

;;; the battlefields one by one, at each step choosing how

;;; many armies to devote to each battlefield. There are a

;;; fixed number of battlefields.

;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; ROLE Relations

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



(role white)

(role black)



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; INIT Relations

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



(init (whiteScore 0))

(init (blackScore 0))

(init (whiteArmies 20))

(init (blackArmies 20))

(init (rounds 0))

(init (maxRounds 10))



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; LEGAL Relations

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



(<= (legal white (commit ?x1))

    (true (whiteArmies ?x2))

    (gte ?x2 ?x1))



(<= (legal black (commit ?x1))

    (true (blackArmies ?x2))

    (gte ?x2 ?x1))

        

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; NEXT Relations

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



(<= (next (maxRounds ?x))

    (true (maxRounds ?x)))

    

(<= (next (rounds ?x1))

    (true (rounds ?x))

    (succ ?x ?x1))



(<= (next (whiteScore ?xNext))

    (true (whiteScore ?x))

    (w1 ?score)

    (sum ?x ?score ?xNext)) 



(<= (next (blackScore ?xNext))

    (true (blackScore ?x))

    (b1 ?score)

    (sum ?x ?score ?xNext))



(<= (next (whiteArmies ?xNext))

    (true (whiteArmies ?x))

    (does white (commit ?x1))

    (sum ?xNext ?x1 ?x))



(<= (next (blackArmies ?xNext))

    (true (blackArmies ?x))

    (does black (commit ?x1))

    (sum ?xNext ?x1 ?x))

    

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; TERMINAL Relations

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



(<= terminal

    (true (rounds ?x))

    (true (maxRounds ?x)))



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; GOAL Relations

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



(<= (goal white ?x)

    (true (whiteScore ?x)))



(<= (goal black ?x)

    (true (blackScore ?x)))



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; View Definitions

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



(<= (w1 2)

    (does white (commit ?x1))

    (does black (commit ?y1))

    (gt ?x1 ?y1))

(<= (b1 0)

    (does white (commit ?x1))

    (does black (commit ?y1))

    (gt ?x1 ?y1))

    

(<= (w1 0)

    (does black (commit ?x1))

    (does white (commit ?y1))

    (gt ?x1 ?y1))

(<= (b1 2)

    (does black (commit ?x1))

    (does white (commit ?y1))

    (gt ?x1 ?y1))

    

(<= (w1 1)

    (does white (commit ?x1))

    (does black (commit ?x1)))

(<= (b1 1)

    (does white (commit ?x1))

    (does black (commit ?x1)))



(<= (sum ?x 0 ?x)

    (or (succ ?x ?y) (succ ?y ?x)))

    

(<= (sum ?x ?y ?z)

    (succ ?y0 ?y)

    (succ ?z0 ?z)

    (sum ?x ?y0 ?z0))

    

(<= (gt ?a ?b)

    (index ?x)

    (not (succ ?x 1))

    (sum ?b ?x ?a))



(<= (gte ?a ?b)

    (index ?x)

    (sum ?b ?x ?a))

    

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Static Relations

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



(index 0)

(index 1)

(index 2)

(index 3)

(index 4)

(index 5)

(index 6)

(index 7)

(index 8)

(index 9)

(index 10)

(index 11)

(index 12)

(index 13)

(index 14)

(index 15)

(index 16)

(index 17)

(index 18)

(index 19)

(index 20)



(succ 0 1)

(succ 1 2)

(succ 2 3)

(succ 3 4)

(succ 4 5)

(succ 5 6)

(succ 6 7)

(succ 7 8)

(succ 8 9)

(succ 9 10)

(succ 10 11)

(succ 11 12)

(succ 12 13)

(succ 13 14)

(succ 14 15)

(succ 15 16)

(succ 16 17)

(succ 17 18)

(succ 18 19)

(succ 19 20)

(succ 20 21)

(succ 21 22)

(succ 22 23)

(succ 23 24)

(succ 24 25)

(succ 25 26)

(succ 26 27)

(succ 27 28)

(succ 28 29)

(succ 29 30)

(succ 30 31)

(succ 31 32)

(succ 32 33)

(succ 33 34)

(succ 34 35)

(succ 35 36)

(succ 36 37)

(succ 37 38)

(succ 38 39)

(succ 39 40)

(succ 40 41)

(succ 41 42)

(succ 42 43)

(succ 43 44)

(succ 44 45)

(succ 45 46)

(succ 46 47)

(succ 47 48)

(succ 48 49)

(succ 49 50)

(succ 50 51)

(succ 51 52)

(succ 52 53)

(succ 53 54)

(succ 54 55)

(succ 55 56)

(succ 56 57)

(succ 57 58)

(succ 58 59)

(succ 59 60)

(succ 60 61)

(succ 61 62)

(succ 62 63)

(succ 63 64)

(succ 64 65)

(succ 65 66)

(succ 66 67)

(succ 67 68)

(succ 68 69)

(succ 69 70)

(succ 70 71)

(succ 71 72)

(succ 72 73)

(succ 73 74)

(succ 74 75)

(succ 75 76)

(succ 76 77)

(succ 77 78)

(succ 78 79)

(succ 79 80)

(succ 80 81)

(succ 81 82)

(succ 82 83)

(succ 83 84)

(succ 84 85)

(succ 85 86)

(succ 86 87)

(succ 87 88)

(succ 88 89)

(succ 89 90)

(succ 90 91)

(succ 91 92)

(succ 92 93)

(succ 93 94)

(succ 94 95)

(succ 95 96)

(succ 96 97)

(succ 97 98)

(succ 98 99)

(succ 99 100)
