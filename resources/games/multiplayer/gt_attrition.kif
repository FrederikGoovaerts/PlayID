;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;  Game Theory: Bidding War of Attrition
;;;
;;;  A five-round bidding war. Each player starts with fifty points.
;;;  During each round, both players bid points in order to win ten
;;;  points. Both players must pay the amount that the loser bids,
;;;  and the winner gets the ten points (five to each in a tie).
;;;  At the end of the game, you get as many points as you have.
;;;
;;;  Background: http://en.wikipedia.org/wiki/War_of_attrition_(game)
;;;
;;;  GDL BY: Sam Schreiber
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role white)
(role black)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; BASE & INPUT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (input ?r (bid ?x)) (role ?r) (index ?x))
(<= (base (whiteScore ?x)) (index ?x))
(<= (base (blackScore ?x)) (index ?x))
(base (round 0))
(base (round 1))
(base (round 2))
(base (round 3))
(base (round 4))
(base (round 5))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Constants
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(maxRounds 5)																																																																																																																																																																																																																																																																																																																																	

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (whiteScore 50))
(init (blackScore 50))
(init (round 0))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal white (bid ?x1))
    (true (whiteScore ?x2))
    (gte ?x2 ?x1))

(<= (legal black (bid ?x1))
    (true (blackScore ?x2))
    (gte ?x2 ?x1))
        
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (round ?x1))
    (true (round ?x))
    (succ ?x ?x1))

(<= (next (whiteScore ?xNext))
    (true (whiteScore ?x))
    (does white (bid ?x1))
    (does black (bid ?y1))
    (min ?x1 ?y1 ?minBid)
    (w1 ?score)
    (sum ?x2 ?minBid ?x)
    (sum ?x2 ?score ?xNext))
    
(<= (next (blackScore ?xNext))
    (true (blackScore ?x))
    (does white (bid ?x1))
    (does black (bid ?y1))
    (min ?x1 ?y1 ?minBid)
    (b1 ?score)
    (sum ?x2 ?minBid ?x)
    (sum ?x2 ?score ?xNext))
        
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
    (true (round ?x))
    (maxRounds ?x))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; GOAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal white ?x)
    (true (whiteScore ?x)))

(<= (goal black ?x)
    (true (blackScore ?x)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; View Definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (min ?a ?a ?a)
    (index ?a))

(<= (min ?a ?b ?a)
    (gt ?b ?a))

(<= (min ?b ?a ?a)
    (gt ?b ?a))

(<= (w1 10)
    (does white (bid ?x1))
    (does black (bid ?y1))
    (gt ?x1 ?y1))
(<= (b1 0)
    (does white (bid ?x1))
    (does black (bid ?y1))
    (gt ?x1 ?y1))
    
(<= (w1 0)
    (does black (bid ?x1))
    (does white (bid ?y1))
    (gt ?x1 ?y1))
(<= (b1 10)
    (does black (bid ?x1))
    (does white (bid ?y1))
    (gt ?x1 ?y1))
    
(<= (w1 5)
    (does white (bid ?x1))
    (does black (bid ?x1)))
(<= (b1 5)
    (does white (bid ?x1))
    (does black (bid ?x1)))

(<= (sum ?x 0 ?x)
    (index ?x))
    
(<= (sum ?x ?y ?z)
    (succ ?y0 ?y)
    (succ ?z0 ?z)
    (sum ?x ?y0 ?z0))
    
(<= (gt ?a ?b)
    (index ?x)
    (not (succ ?x 1))
    (sum ?b ?x ?a))

(<= (gte ?a ?b)
    (index ?x)
    (sum ?b ?x ?a))
    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Static Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(index 0)
(index 1)
(index 2)
(index 3)
(index 4)
(index 5)
(index 6)
(index 7)
(index 8)
(index 9)
(index 10)
(index 11)
(index 12)
(index 13)
(index 14)
(index 15)
(index 16)
(index 17)
(index 18)
(index 19)
(index 20)
(index 21)
(index 22)
(index 23)
(index 24)
(index 25)
(index 26)
(index 27)
(index 28)
(index 29)
(index 30)
(index 31)
(index 32)
(index 33)
(index 34)
(index 35)
(index 36)
(index 37)
(index 38)
(index 39)
(index 40)
(index 41)
(index 42)
(index 43)
(index 44)
(index 45)
(index 46)
(index 47)
(index 48)
(index 49)
(index 50)
(index 51)
(index 52)
(index 53)
(index 54)
(index 55)
(index 56)
(index 57)
(index 58)
(index 59)
(index 60)
(index 61)
(index 62)
(index 63)
(index 64)
(index 65)
(index 66)
(index 67)
(index 68)
(index 69)
(index 70)
(index 71)
(index 72)
(index 73)
(index 74)
(index 75)
(index 76)
(index 77)
(index 78)
(index 79)
(index 80)
(index 81)
(index 82)
(index 83)
(index 84)
(index 85)
(index 86)
(index 87)
(index 88)
(index 89)
(index 90)
(index 91)
(index 92)
(index 93)
(index 94)
(index 95)
(index 96)
(index 97)
(index 98)
(index 99)
(index 100)

(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)
(succ 27 28)
(succ 28 29)
(succ 29 30)
(succ 30 31)
(succ 31 32)
(succ 32 33)
(succ 33 34)
(succ 34 35)
(succ 35 36)
(succ 36 37)
(succ 37 38)
(succ 38 39)
(succ 39 40)
(succ 40 41)
(succ 41 42)
(succ 42 43)
(succ 43 44)
(succ 44 45)
(succ 45 46)
(succ 46 47)
(succ 47 48)
(succ 48 49)
(succ 49 50)
(succ 50 51)
(succ 51 52)
(succ 52 53)
(succ 53 54)
(succ 54 55)
(succ 55 56)
(succ 56 57)
(succ 57 58)
(succ 58 59)
(succ 59 60)
(succ 60 61)
(succ 61 62)
(succ 62 63)
(succ 63 64)
(succ 64 65)
(succ 65 66)
(succ 66 67)
(succ 67 68)
(succ 68 69)
(succ 69 70)
(succ 70 71)
(succ 71 72)
(succ 72 73)
(succ 73 74)
(succ 74 75)
(succ 75 76)
(succ 76 77)
(succ 77 78)
(succ 78 79)
(succ 79 80)
(succ 80 81)
(succ 81 82)
(succ 82 83)
(succ 83 84)
(succ 84 85)
(succ 85 86)
(succ 86 87)
(succ 87 88)
(succ 88 89)
(succ 89 90)
(succ 90 91)
(succ 91 92)
(succ 92 93)
(succ 93 94)
(succ 94 95)
(succ 95 96)
(succ 96 97)
(succ 97 98)
(succ 98 99)
(succ 99 100)
